pluginManagement {
    repositories {
        gradlePluginPortal()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://repo.spring.io/release" }
        maven { url "https://maven.aliyun.com/repository/public" }
    }
}

plugins {
    id "com.gradle.enterprise" version "3.13.3"
}

gradleEnterprise {
    buildScan {
        termsOfServiceUrl = "https://gradle.com/terms-of-service"
        termsOfServiceAgree = "yes"
        publishOnFailure()
    }
}

rootProject.name = "spring-boot-extension"

fileTree(rootDir) {
    def excludes = gradle.startParameter.projectProperties.get("excludeProjects")?.split(",")
    include "**/*.gradle"
    exclude "build", "**/gradle", "settings.gradle", "buildSrc", "/build.gradle", ".", "out"
    if (excludes) {
        exclude excludes
    }
}.forEach {
    def buildFilePath = it.parentFile.absolutePath
    def projectPath = buildFilePath.replace(rootProject.projectDir.absolutePath, "").replace(File.separator, ":")
    include projectPath

    def project = findProject(projectPath)
    project.projectDir = it.parentFile
    project.buildFileName = it.name
}

gradle.settingsEvaluated {
    if (JavaVersion.current() < JavaVersion.VERSION_17) {
        throw new GradleException("This build requires JDK 17. It's currently ${JavaVersion.current()}. You can ignore this check by passing '-Dorg.gradle.ignoreBuildJavaVersionCheck'.")
    }
}
