pluginManagement {
    repositories {
        gradlePluginPortal()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://repo.spring.io/release" }
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
        maven { url "https://maven.aliyun.com/repository/public" }
        maven { url "https://repo.spring.io/libs-snapshot/" }
    }
}

rootProject.name = "spring-boot-example"

fileTree(rootDir) {
    def excludes = gradle.startParameter.projectProperties.get("excludeProjects")?.split(",")
    include "**/*.gradle"
    exclude "build", "**/gradle", "settings.gradle", "buildSrc", "/build.gradle", ".", "out"
    if (excludes) {
        exclude excludes
    }
}.forEach {
    def buildFilePath = it.parentFile.absolutePath
    def projectPath = buildFilePath.replace(rootDir.absolutePath, "").replace(File.separator, ":")
    include projectPath

    def project = findProject(projectPath)
    project.projectDir = it.parentFile
    project.buildFileName = it.name
}

gradle.settingsEvaluated {
    if (JavaVersion.current() < JavaVersion.VERSION_17) {
        throw new GradleException("This build requires JDK 17. It's currently ${JavaVersion.current()}. You can ignore this check by passing '-Dorg.gradle.ignoreBuildJavaVersionCheck'.")
    }
}
