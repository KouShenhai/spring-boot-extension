pluginManagement {
    repositories {
        gradlePluginPortal()
        maven { url 'https://plugins.gradle.org/m2/' }
        maven { url 'https://repo.spring.io/release' }
        maven { url "https://repo.spring.io/snapshot" }
        maven { url 'https://repo.spring.io/milestone' }
        maven { url 'https://maven.aliyun.com/repository/public' }
        maven { url 'https://repo.spring.io/libs-snapshot/' }
    }
}

rootProject.name = 'Spring-Boot-Demo'

def buildFiles = fileTree(rootDir) {
    def excludes = gradle.startParameter.projectProperties.get('excludeProjects')?.split(',')
    include '**/*.gradle'
    exclude 'build', '**/gradle', 'settings.gradle', 'buildSrc', '/build.gradle', '.', 'out'
    if (excludes) {
        exclude excludes
    }
}

buildFiles.forEach { buildFile ->
    def buildFilePath = buildFile.parentFile.absolutePath
    def projectPath = buildFilePath.replace(rootDir.absolutePath, "").replace(File.separator, ":")
    include projectPath
}
