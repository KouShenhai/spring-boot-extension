import org.springframework.boot.gradle.plugin.SpringBootPlugin

def versionCatalog = rootProject.extensions
        .getByType(VersionCatalogsExtension.class)
        .named("libs")

dependencies {
    // 暂时升级、等待springboot-3
    api enforcedPlatform("org.springframework:spring-framework-bom:6.0.0")
    api platform(SpringBootPlugin.BOM_COORDINATES)
    versionCatalog.libraryAliases
            .findAll { it.endsWith("dependencies") || it.endsWith("bom") }
            .collect { versionCatalog.findLibrary(it).get().get() }
            .forEach { api platform(it) }
    constraints {
        versionCatalog.libraryAliases
                .findAll { !(it.endsWith("plugin") || it.endsWith("dependencies") || it.endsWith("bom")) }
                .collect { versionCatalog.findLibrary(it).get().get() }
                .forEach { api it }
        parent.commonModuleProjects.forEach { api(it) }
    }
}
