import org.gradle.util.GradleVersion

apply plugin: "groovy-gradle-plugin"

repositories {
    maven { url "https://repo.spring.io/milestone" }
    maven { url "https://maven.aliyun.com/repository/public" }
    maven { url "https://plugins.gradle.org/m2/" }
    maven { url "https://repo.spring.io/release" }
    maven { url "https://repo.spring.io/libs-snapshot/" }
}

ext {
    bootVersion = libs.versions.springBoot.get()
}

dependencies {
    implementation "org.springframework.boot:spring-boot-gradle-plugin:$bootVersion"
    implementation "org.springframework:spring-core:6.0.0"
}

gradlePlugin {
    plugins {
        bomPlugin {
            id = "com.livk.bom"
            implementationClass = "com.livk.boot.BomPlugin"
        }
        modulePlugin {
            id = "com.livk.module"
            implementationClass = "com.livk.boot.ModulePlugin"
        }
        commonPlugin {
            id = "com.livk.common"
            implementationClass = "com.livk.boot.CommonPlugin"
        }
        rootProjectPlugin {
            id = "com.livk.root"
            implementationClass = "com.livk.boot.RootProjectPlugin"
        }
        servicePlugin {
            id = "com.livk.service"
            implementationClass = "com.livk.boot.ServicePlugin"
        }
    }
}

(project.tasks.getByName(JavaPlugin.JAR_TASK_NAME) as Jar).manifest {
    attributes(
            [
                    "Created-By"    : System.getProperty("java.version") + " (" +
                            System.getProperty("java.specification.vendor") + ")",
                    "Gradle-Version": GradleVersion.current()
            ]
    )
}
