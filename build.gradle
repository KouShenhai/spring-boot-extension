plugins {
    id "com.livk.dependency"
    id "org.springframework.boot" apply false
}

ext {
    bootVersion = libs.versions.springBoot.get()
    bom = List.of(project(":livk-boot-dependencies"), project(":livk-boot-bom"))
    gradleModuleProjects = (subprojects.findAll { it.buildFile.exists() } - bom)
    commonModuleProjects = gradleModuleProjects.findAll {
        it.name.endsWith("-common") ||
                it.name.endsWith("-starter") ||
                it.name.endsWith("-autoconfigure")
    }
    springModuleProjects = (gradleModuleProjects - commonModuleProjects)
}

configure(bom) {
    apply plugin: "java-platform"
    apply plugin: "com.livk.maven.deployed"

    project.extensions.getByType(JavaPlatformExtension.class).allowDependencies()
}

configure(commonModuleProjects) {
    apply plugin: "com.livk.maven.deployed"
    apply plugin: "com.livk.resources"
}

configure(springModuleProjects) {
    apply plugin: "com.livk.build.boot"
}

configure(allprojects) {
    apply plugin: "com.livk.clean.expand"
    apply plugin: "com.livk.all.configuration"
    repositories {
        maven { url "https://repo.spring.io/milestone" }
        maven { url "https://maven.aliyun.com/repository/public" }
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://repo.spring.io/release" }
        maven { url "https://repo.spring.io/libs-snapshot/" }
    }
}

configure(gradleModuleProjects) {
    apply plugin: "com.livk.compile-args"
    apply plugin: "com.livk.dependency"

    dependencies {
        dependencyBom platform(project(":livk-boot-dependencies"))

        compileOnly "org.projectlombok:lombok"
        annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
        annotationProcessor "org.projectlombok:lombok"
        testImplementation "org.springframework:spring-tx"
        testImplementation "org.springframework.boot:spring-boot-starter-test"
        testAnnotationProcessor "org.projectlombok:lombok"
        testCompileOnly "org.projectlombok:lombok"
    }
}
