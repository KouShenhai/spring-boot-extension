import org.springframework.boot.gradle.plugin.SpringBootPlugin

import java.time.Instant
import java.util.concurrent.TimeUnit

plugins {
    id "java-library"
    id "org.springframework.boot" version("$springBootVersion") apply false
}

ext {
    bom = project(":livk-boot-dependencies")
    gradleModuleProjects = subprojects.findAll {
        it.buildFile.exists()
    } - bom
    commonModuleProjects = gradleModuleProjects.findAll {
        it.name.endsWith("-common") || it.name.endsWith("-starter")
    }
    springModuleProjects = gradleModuleProjects - commonModuleProjects
}

configure(commonModuleProjects) {
    apply plugin: "maven-publish"

    publishing {
        repositories {
            mavenLocal()
        }
        publications {
            library(MavenPublication) {
                artifact jar
            }
        }
    }
}

configure(springModuleProjects) {
    apply plugin: "org.springframework.boot"

    springBoot {
        buildInfo {
            properties {
                group = "$project.group"
                version = "$project.version"
                time = Instant.now().plusMillis(TimeUnit.HOURS.toMillis(8))
            }
        }
    }

}

configure(allprojects) {
    repositories {
        maven { url "https://repo.spring.io/milestone" }
        maven { url "https://maven.aliyun.com/repository/public" }
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://repo.spring.io/release" }
        maven { url "https://repo.spring.io/libs-snapshot/" }
    }

    task cleanBuildDir(type: Delete) {
        allprojects.forEach {
            if (!it.buildFile.exists()) {
                delete "$projectDir/build"
                delete "$projectDir/out"
                delete "$projectDir/bin"
            }
        }
    }
}

configure(gradleModuleProjects) {
    apply plugin: "java-library"
    apply plugin: "io.spring.javaformat"
    apply plugin: "livk.resources.plugin"
    apply from: "$rootDir/gradle/dependency.gradle"

    configurations {
        dependencyBom {
            api.extendsFrom(dependencyBom)
            implementation.extendsFrom(dependencyBom)
            compileOnly.extendsFrom(dependencyBom)
            annotationProcessor.extendsFrom(dependencyBom)
            testImplementation.extendsFrom(dependencyBom)
            testCompileOnly.extendsFrom(dependencyBom)
            testAnnotationProcessor.extendsFrom(dependencyBom)
        }
    }

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    dependencies {
        dependencyBom platform(SpringBootPlugin.BOM_COORDINATES)
        dependencyBom platform(project(":livk-boot-dependencies"))
    }
}
